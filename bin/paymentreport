#!/usr/bin/perl
use strict;
use DBI;
use Text::xSV;

use Act::Config;
use Act::Email;
use Act::Util;

# command line arguments
my $recipient = shift or usage();
my @confs = @ARGV or usage();

# connect to database
Act::Util::db_connect();

# CSV file
my $csv = Text::xSV->new;
my $sth = $Request{dbh}->prepare(
    'SELECT o.order_id, o.user_id, o.conf_id, o.datetime, u.first_name, u.last_name, u.email, o.amount, o.currency, o.means'
  . ' FROM orders o, users u'
  . ' WHERE o.user_id = u.user_id AND o.status = ?'
  . ' AND o.conf_id IN (' . join(',', map '?', @confs) . ')'
  . ' ORDER BY o.datetime'
);
$sth->execute('paid', @confs);
my $body = $csv->format_row(qw(order_id user_id conf_id datetime first_name last_name email amount currency means));
while (my @row = $sth->fetchrow_array) {
    $body .= $csv->format_row(@row);
}

# send email
Act::Email::send(
    content_type => 'text/csv',
    from     => { name => 'Act', email => 'echo@mongueurs.net' },
    to       => $recipient,
    subject  => "Act payment report",
    xheaders => { 'X-Act' => 'payment report' },
    body     => $body,
);

sub usage
{
    die "Usage: $0 email conf_id [conf_id...]\n"
}
